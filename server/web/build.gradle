buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.bmuschko:gradle-cargo-plugin:2.1"
    }
}

apply plugin: 'war'
apply plugin: 'com.bmuschko.cargo'


configurations {
    wildflyDist
}

dependencies {

    compile project(':ejb')

    compile 'org.jboss.resteasy:resteasy-jackson-provider:3.0.10.Final'
//    compile group: 'org.wildfly', name: 'wildfly-spec-api', version: libraryVersions.wildfly

    // REST
    testCompile "javax.ws.rs:javax.ws.rs-api:2.0.1"
    testCompile "org.jboss.resteasy:resteasy-client:3.0.10.Final"
    // Wildfly
    testCompile group: 'org.jboss.shrinkwrap.resolver', name: 'shrinkwrap-resolver-depchain', version: '2.2.0-beta-1'
    testCompile group: 'org.jboss.arquillian', name: 'arquillian-bom', version: '1.1.5.Final'
    testCompile group: 'org.jboss.arquillian.protocol', name: 'arquillian-protocol-servlet', version: '1.1.5.Final'
    testCompile group: 'org.jboss.arquillian.junit', name: 'arquillian-junit-container', version: '1.1.5.Final'
    testCompile group: 'org.wildfly', name: 'wildfly-arquillian-container-embedded', version: libraryVersions.wildfly
    testCompile group: 'org.jboss.shrinkwrap.resolver', name: 'shrinkwrap-resolver-gradle-depchain', version: '2.2.0-beta-1'


    testRuntime group: 'org.jboss.logging', name: 'jboss-logging', version: '3.2.0.Final'

    wildflyDist group: 'org.wildfly', name: 'wildfly-dist', version: libraryVersions.wildfly, ext: 'zip'


}

/**
 * Install wildfly server for integration tests
 */
task downloadWildfly << {
    println "Downloading wildfly ${libraryVersions.wildfly} to target-folder ${rootDir}/build/"

    def zipArchives = configurations.wildflyDist.resolve();

    zipArchives.each {
        File f = new File(it.getAbsolutePath())
        copy {
            from zipTree(f)
            into "$buildDir"
        }
    }

    println " Copy and extracting successful. Happy testing! "

}

test {

    /**
     * Installation of JBOSS to use for testing, this can be set here
     * or in the arquillian.xml.
     */
    environment 'JBOSS_HOME', hasProperty('jbossHome') ? jbossHome : "${rootDir}/web/build/wildfly-${libraryVersions.wildfly}"

    /**
     * WildFly embedded default logging
     */
    systemProperty 'java.util.logging.manager', 'org.jboss.logmanager.LogManager'

    /**
     * Ports for wildfly embedded server
     */
    systemProperty 'jboss.management.http.port', '9990'
    systemProperty 'jboss.http.port', '8080'


}

cargoRunLocal.dependsOn assemble, downloadWildfly

cargo {
    containerId = 'wildfly8x'

    deployable {
        context = 'aemon'
    }

    local {
        containerProperties {
            outputFile = file('build/wildfly.log')
            homeDir = file("${rootDir}/web/build/wildfly-${libraryVersions.wildfly}")
        }
    }

}